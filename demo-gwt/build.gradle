buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'org.gretty:gretty:3.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'org.gretty'

archivesBaseName = 'jmini3d-gwt_demo'

dependencies {
    implementation 'com.google.gwt:gwt-user:2.9.0'
    implementation 'com.google.gwt:gwt-dev:2.9.0'
    implementation project(':core')
    implementation project(path: ':core', configuration: 'archives')
    implementation project(':gwt')
    implementation project(path: ':gwt', configuration: 'archives')
    implementation project(':demo-common')
    implementation project(path: ':demo-common', configuration: 'archives')
}

task compileGwt(dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.projectDir}/src/main/webapp/"
    ext.extraDir = "${project.buildDir}/extra"

    sourceSets.main.java.srcDirs.each{ dir ->
        inputs.dir dir
    }
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    doFirst {
        file(buildDir).mkdirs()
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.java.outputDir,         // Generated classes
                sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args =
            [
                    'jmini3d.gwt.demo',
                    '-war', buildDir,
                    '-extra', extraDir,
                    '-logLevel', 'INFO',
                    '-localWorkers', '2',
                    '-style', 'PRETTY',
                    //'-optimize', '9',        // No effect?
                    '-XdisableCastChecking',   // Disables runtime checking of cast operations. This can reduce compiled file sizes.
                    '-XdisableClassMetadata',  // Disables some java.lang.Class methods. This can reduce compiled file sizes.
                    //'-XenableClosureCompiler', // This should create smaller JavaScript output. Not compatible with the compile report
                    //'-compileReport',
                    //'-XsoycDetailed',
                    //'-XcompilerMetrics',
            ]

    maxHeapSize = '512M'
}

jar.dependsOn compileGwt

task gwtSuperDev(dependsOn: classes, type: JavaExec) {

    main = 'com.google.gwt.dev.codeserver.CodeServer'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.runtimeClasspath,       // Deps
        ]
    }

    args =
            [
                    'jmini3d.gwt.demo',
            ]

    maxHeapSize = '512M'
}

task copyResources(type: Copy)

copyResources {
    from '../demo-common/resources'
    into 'src/main/webapp/resources'
    include('**/*')
}

compileGwt.dependsOn copyResources

task copyShaders(type: Copy)

copyShaders {
    from '../core/shaders'
    into 'src/main/webapp/shaders'
    include('**/*')
}

compileJava.dependsOn copyShaders

gretty {
    servletContainer = 'jetty7'
    contextPath = '/jmini3d'
}