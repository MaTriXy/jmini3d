apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

archivesBaseName = 'jmini3d-android'

dependencies {
    api project(':core')
}

android {
    compileSdkVersion 32
    defaultConfig {
        versionCode 1
        versionName "1.0"

        targetSdkVersion 32
        // Android 2.2 (=8) has VBO problems for versions < 2.3 we will use OpenGL ES 1.1 -->
        minSdkVersion 9
    }
    namespace 'jmini3d.android'
}

task copyResources(type: Copy)

copyResources {
    from '../core/shaders'
    into 'src/main/res/raw'
    include('**/*')
}

preBuild.dependsOn copyResources

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main/java'
}

publishToMavenLocal.dependsOn ':android:bundleReleaseAar'

publishing {
    publications {
        jmini3dAndroid(MavenPublication) {
            artifactId = 'jmini3d-android'
            artifact sourcesJar
            artifact('build/outputs/aar/jmini3d-android-release.aar')
        }
    }
}
