buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'jmini3d-android'

dependencies {
    api project(':core')
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        // Android 2.2 (=8) has VBO problems for versions < 2.3 we will use OpenGL ES 1.1 -->
        minSdkVersion 9
    }
}

task copyResources(type: Copy)

copyResources {
    from '../core/shaders'
    into 'src/main/res/raw'
    include('**/*')
}

preBuild.dependsOn copyResources

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main/java'
}

publishToMavenLocal.dependsOn 'assembleRelease'
bintrayUpload.dependsOn 'assembleRelease'

publishing {
    publications {
        jmini3dAndroid(MavenPublication) {
            artifactId = 'jmini3d-android'
            artifact sourcesJar
            artifact('build/outputs/aar/jmini3d-android-release.aar')
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['jmini3dAndroid']
    pkg {
        repo = 'maven'
        name = 'com.mobialia.jmini3d-android'
        desc = 'Minimalistic OpenGL2 3D engine for mobile apps that supports Android and GWT'
        userOrg = 'mobialia'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mobialia/jmini3d.git'
        githubRepo = 'mobialia/jmini3d'
        publish = true
    }
}